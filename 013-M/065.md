0xMojito

medium

# Hat level can become too large and DOS other functions

## Summary
https://github.com/Hats-Protocol/hats-protocol/blob/fafcfdf046c0369c1f9e077eacd94a328f9d7af0/src/HatsIdUtilities.sol#L96-L106

Hat trees are allowed to link to each other using `linkedTreeAdmins` mapping.
If there are many links in 1 tree, some hats in this tree will have too large level and break the block gas limit when trying to access.

## Vulnerability Detail
In Hats contract, it is allowed to link a top local hat to another hat. It means it will become children of the linked hat. For example, A is a hat at level 0 (top local), B is a hat at level 4 of another tree. If A is linked to B, then A is now at level 5, children of A are at level 6 and so on. 

Now consider checking admin functionality, it will do at least 2 external call (1 to toggle and 1 to eligibility) at each level up to the top hat. Each external call will consume around 20k gas. Block gas limit of Ethereum is 30 million so number of trees required to break the gas limit is
```solidity
blockGasLimit = 30 million
gasCostEachLevel = 2 * 20k = 40k
levels = 30 mil / 40k = 750 
trees = 750 / 15 = 50
```
So if around 50 trees are linked, it will break the block gas limit when top hat trying to execute any function on bottom hat. 

## Impact
Top hat will lose its control over bottom hat.

## Code Snippet
Function `isAdminOfHat()` call `isWearerOfHat()` at each level, which then call `_isActive()` and `_isEligible()`
```solidity
function isAdminOfHat(address _user, uint256 _hatId) public view returns (bool isAdmin) {
    uint256 linkedTreeAdmin;
    uint32 adminLocalHatLevel;
    if (isLocalTopHat(_hatId)) {
        linkedTreeAdmin = linkedTreeAdmins[getTopHatDomain(_hatId)];
        if (linkedTreeAdmin == 0) {
            // tree is not linked
            return isAdmin = isWearerOfHat(_user, _hatId);
        } else {
            // tree is linked
            if (isWearerOfHat(_user, linkedTreeAdmin)) {
                return isAdmin = true;
            } // user wears the treeAdmin
            else {
                adminLocalHatLevel = getLocalHatLevel(linkedTreeAdmin);
                _hatId = linkedTreeAdmin;
            }
        }
    } else {
        // if we get here, _hatId is not a tophat of any kind
        // get the local tree level of _hatId's admin
        adminLocalHatLevel = getLocalHatLevel(_hatId) - 1;
    }

    // search up _hatId's local address space for an admin hat that the _user wears
    while (adminLocalHatLevel > 0) {
        if (isWearerOfHat(_user, getAdminAtLocalLevel(_hatId, adminLocalHatLevel))) {
            return isAdmin = true;
        }
        // should not underflow given stopping condition > 0
        unchecked {
            --adminLocalHatLevel;
        }
    }

    // if we get here, we've reached the top of _hatId's local tree, ie the local tophat
    // check if the user wears the local tophat
    if (isWearerOfHat(_user, getAdminAtLocalLevel(_hatId, 0))) return isAdmin = true;

    // if not, we check if it's linked to another tree
    linkedTreeAdmin = linkedTreeAdmins[getTopHatDomain(_hatId)];
    if (linkedTreeAdmin == 0) {
        // tree is not linked
        // we've already learned that user doesn't wear the local tophat, so there's nothing else to check; we return false
        return isAdmin = false;
    } else {
        // tree is linked
        // check if user is wearer of linkedTreeAdmin
        if (isWearerOfHat(_user, linkedTreeAdmin)) return true;
        // if not, recurse to traverse the parent tree for a hat that the user wears
        isAdmin = isAdminOfHat(_user, linkedTreeAdmin);
    }
}
```

## Tool used

Manual Review

## Recommendation
Consider adding a upper threshold when linking hats
