roguereddwarf

medium

# Signers can backdoor the Safe by swapping modules to execute any transaction in the future without consensus

## Summary
In the previous audit report by Trust Security it has been pointed out in the issue with title `TRST-M-6 Signers can backdoor the safe to execute any transaction in the future without consensus` that signers should not be allowed to add modules to the Safe.

The reason that modules should not be allowed to be added by signers is that modules can execute any transaction without further safety checks by the HatsSignerGate.

The issue has been accepted by the sponsor and mitigated by checking that in between calls to `HatsSignerGateBase.checkTransaction` and `HatsSignerGateBase.checkAfterExecution` the modules registered in the Safe have not been modified.

The issue I found is that the signers are still able to enable their own Module which enables them to have a backdoor.
It is possible to bypass the checks implemented in the HatsSignerGate.

The root cause is that a transaction by the signers can reenter the `HatsSignerGateBase.checkTransaction` function.

My recommended mitigation therefore consists of an additional reentrancy check.

## Vulnerability Detail
When signers execute a transaction on the Safe via the [Safe.execTransaction](https://github.com/safe-global/safe-contracts/blob/cb22537c89ea4187f4ad141ab2e1abf15b27416b/contracts/Safe.sol#L135-L217) function, the Safe calls `HatsSignerGateBase.checkTransaction` before executing the transaction and calls `HatsSignerGateBase.checkAfterExecution` after executing the transaction.

The `HatsSignerGateBase.checkTransaction` function saves a hash of the enabled modules:
[Link](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L497-L498)
```solidity
(address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount);
_existingModulesHash = keccak256(abi.encode(modules));
```

After the Safe has executed the transaction that the signers sent, the `HatsSignerGateBase.checkAfterExecution` function is called which also generates a hash of the enabled modules and compares it to the saved hash:
[Link](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L522-L525)
```solidity
(address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount + 1);
if (keccak256(abi.encode(modules)) != _existingModulesHash) {
    revert SignersCannotChangeModules();
}
```

Thereby it is ensured that the enabled modules have not been modified in between both calls.

The issue is that the signers can execute a transaction that does the following:

1. disable a module
2. register their malicious backdoor module instead
3. Call `HatsSignerGateBase.checkTransaction` such that modules hash is set to the hash of the modified modules

When the transaction that performs these 3 steps is executed, the `HatsSignerGateBase.checkAfterExecution` function is called by the Safe.
This function will now be unable to detect that the modules have changed.

I will now walk through the attack in more detail.

So it all starts with the signers executing a transaction on the Safe via the `Safe.execTransaction` function:
[Link](https://github.com/safe-global/safe-contracts/blob/cb22537c89ea4187f4ad141ab2e1abf15b27416b/contracts/Safe.sol#L135-L146)
```solidity
    function execTransaction(
        address to,
        uint256 value,
        bytes calldata data,
        Enum.Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address payable refundReceiver,
        bytes memory signatures
    ) public payable virtual returns (bool success) {
```

The `operation` parameter needs to be `Enum.Operation.DelegateCall`.
The transaction performs a delegatecall into a contract that the signers have deployed which then performs the attack. It is important that this is a delegatecall because the attack requires that the `msg.sender` is the address of the Safe, not the address of the deployed contract.

So the attacker contract then calls [`ModuleManager.disableModule`](https://github.com/safe-global/safe-contracts/blob/131f0d25135c1b98c185c940ae37fb0275ac4062/contracts/base/ModuleManager.sol#L62-L69) to disable a module and [`ModuleManager.enableModule`](https://github.com/safe-global/safe-contracts/blob/131f0d25135c1b98c185c940ae37fb0275ac4062/contracts/base/ModuleManager.sol#L46-L54) to enable a malicious backdoor module.

The attacker contract then calls `HatsSignerGateBase.checkTransaction` using signatures that the signers have provided (as the `checkTransaction` function needs to be called with valid signatures). It is possible for the signers to generate such signatures since all necessary parametes are known to them or can even be chosen by them (parameters for the `checkTransaction` function). They can just decide to use the same signatures as for the actual transaction on the Safe.

We can see that in the `checkTransaction` function a new modules hash is generated:
[Link](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L497-L498)
```solidity
(address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount);
_existingModulesHash = keccak256(abi.encode(modules));
```

So the subsequent call to `checkAfterExecution` will find that the modules have not been altered since the hash stayed the same.

It was thereby possible to enable a malicious backdoor module.

## Impact
Signers can enable a module that serves them as a backdoor and allows to execute future transactions without consensus.

## Code Snippet
https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L445-L503
```solidity
    function checkTransaction(
        address to,
        uint256 value,
        bytes calldata data,
        Enum.Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address payable refundReceiver,
        bytes memory signatures,
        address // msgSender
    ) external override {
        if (msg.sender != address(safe)) revert NotCalledFromSafe();


        uint256 safeOwnerCount = safe.getOwners().length;
        // uint256 validSignerCount = _countValidSigners(safe.getOwners());


        // ensure that safe threshold is correct
        reconcileSignerCount();


        if (safeOwnerCount < minThreshold) {
            revert BelowMinThreshold(minThreshold, safeOwnerCount);
        }


        // get the tx hash; view function
        bytes32 txHash = safe.getTransactionHash(
            // Transaction info
            to,
            value,
            data,
            operation,
            safeTxGas,
            // Payment info
            baseGas,
            gasPrice,
            gasToken,
            refundReceiver,
            // Signature info
            // We subtract 1 since nonce was just incremented in the parent function call
            safe.nonce() - 1 // view function
        );


        uint256 validSigCount = countValidSignatures(txHash, signatures, signatures.length / 65);


        // revert if there aren't enough valid signatures
        if (validSigCount < safe.getThreshold() || validSigCount < minThreshold) {
            revert InvalidSigners();
        }


        // record existing modules for post-flight check
        // SENTINEL_OWNERS and SENTINEL_MODULES are both address(0x1)
        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount);
        _existingModulesHash = keccak256(abi.encode(modules));


        unchecked {
            ++_guardEntries;
        }
    }
```

## Tool used
Manual Review

## Recommendation
As stated above, my recommended mitigation is to simply make sure that the `checkTransaction` function cannot be reentered while a transaction of the Safe is in progress.

Fix:
```diff
diff --git a/src/HatsSignerGateBase.sol b/src/HatsSignerGateBase.sol
index 3e8bb5f..cd8899d 100644
--- a/src/HatsSignerGateBase.sol
+++ b/src/HatsSignerGateBase.sol
@@ -500,6 +500,10 @@ abstract contract HatsSignerGateBase is BaseGuard, SignatureDecoder, HatsOwnedIn
         unchecked {
             ++_guardEntries;
         }
+
+        if (_guardEntries > 1) {
+            revert Reentrancy();
+        }
     }
```