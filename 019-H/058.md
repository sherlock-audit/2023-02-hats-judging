roguereddwarf

high

# Safe can no longer execute transactions when module other than HatsSignerCreate enables a module

## Summary
When a transaction is executed via the [Safe.execTransaction](https://github.com/safe-global/safe-contracts/blob/cb22537c89ea4187f4ad141ab2e1abf15b27416b/contracts/Safe.sol#L135-L217) function, the HatsSignerGate checks the modules that are enabled in the Safe before and after execution of the transaction.

The current implementation only works when any additional modules are enabled via the [HatsSignerGateBase.enableNewModule](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L165-L179) function.

The issue arises when an additional module is enabled via another module which is not the HatsSignerGate.

For example the HatsSignerGate can enable a Module A. When Module A then enables another Module B the code breaks.

If Module A is not able to disable the module and Module B cannot disable itself (Module A and Module B might not implement such a functionality) then legitimate transactions by the signers will fail.


## Vulnerability Detail
First we must understand how the HatsSignerGate keeps track of enabled modules and how it uses this information to verify that a transaction that is executed on the Safe does not change the enabled modules.

The HatsSignerGate sets the `enabledModuleCount` when it is set up:
[Link](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L83)
```solidity
enabledModuleCount = _existingModuleCount + 1; // this contract is enabled as well
```

It then increments `enabledModuleCount` whenever `HatsSignerGateBase.enableNewModule` is called:
[Link](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L165-L179)
```solidity
    function enableNewModule(address _module) external onlyOwner {
        ++enabledModuleCount;


        bytes memory data = abi.encodeWithSignature("enableModule(address)", _module);
        bool success = safe.execTransactionFromModule(
            address(safe), // to
            0, // value
            data, // data
            Enum.Operation.Call // operation
        );


        if (!success) {
            revert FailedExecEnableModule();
        }
    }
```

When a transaction is executed on the Safe, first the `HatsSignerGateBase.checkTransaction` function is called which reads `enabledModuleCount` modules from the Safe and saves the hash:
[Link](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L497-L498)
```solidity
        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount);
        _existingModulesHash = keccak256(abi.encode(modules));
```

After the transaction is executed the `HatsSignerGateBase.checkAfterExecution` function is called which reads `enabledModuleCount + 1` modules from the Safe and compares the hash to the hash that was saved before the transaction:
[Link](https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L522-L525)
```solidity
        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount + 1);
        if (keccak256(abi.encode(modules)) != _existingModulesHash) {
            revert SignersCannotChangeModules();
        }
```

The reason that after the execution `enabledModuleCount + 1` modules are queried is to check that no new modules have been added.
In case modules are only ever registered via the HatsSignerGate the code works correctly.

However let's consider what happens when, as described above, HatsSignerGate enables Module A which then enables Module B:

1. `enabledModuleCount` is only incremented once (when HatsSignerGate enables Module A)
2. Now the signers want to execute a transaction on the Safe
3. When `checkTransaction` is executed, Module B will not be queried (only modules up to and including Module A are queried and used to calculate the hash)
4. When `checkAfterExecution` is executed, Module B is queried (`enabledModuleCount + 1` modules are queried)
5. The code thinks that the transaction has added a new Module and will revert.

## Impact
The Safe can end up in a state where legitimate transactions will fail.

## Code Snippet
https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L445-L503
```solidity
    function checkTransaction(
        address to,
        uint256 value,
        bytes calldata data,
        Enum.Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address payable refundReceiver,
        bytes memory signatures,
        address // msgSender
    ) external override {
        if (msg.sender != address(safe)) revert NotCalledFromSafe();


        uint256 safeOwnerCount = safe.getOwners().length;
        // uint256 validSignerCount = _countValidSigners(safe.getOwners());


        // ensure that safe threshold is correct
        reconcileSignerCount();


        if (safeOwnerCount < minThreshold) {
            revert BelowMinThreshold(minThreshold, safeOwnerCount);
        }


        // get the tx hash; view function
        bytes32 txHash = safe.getTransactionHash(
            // Transaction info
            to,
            value,
            data,
            operation,
            safeTxGas,
            // Payment info
            baseGas,
            gasPrice,
            gasToken,
            refundReceiver,
            // Signature info
            // We subtract 1 since nonce was just incremented in the parent function call
            safe.nonce() - 1 // view function
        );


        uint256 validSigCount = countValidSignatures(txHash, signatures, signatures.length / 65);


        // revert if there aren't enough valid signatures
        if (validSigCount < safe.getThreshold() || validSigCount < minThreshold) {
            revert InvalidSigners();
        }


        // record existing modules for post-flight check
        // SENTINEL_OWNERS and SENTINEL_MODULES are both address(0x1)
        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount);
        _existingModulesHash = keccak256(abi.encode(modules));


        unchecked {
            ++_guardEntries;
        }
    }
```

https://github.com/Hats-Protocol/hats-zodiac/blob/9455cc0957762f5dbbd8e62063d970199109b977/src/HatsSignerGateBase.sol#L507-L529
```solidity
    function checkAfterExecution(bytes32, bool) external override {
        if (msg.sender != address(safe)) revert NotCalledFromSafe();


        if (
            abi.decode(StorageAccessible(address(safe)).getStorageAt(uint256(GUARD_STORAGE_SLOT), 1), (address))
                != address(this)
        ) {
            revert CannotDisableThisGuard(address(this));
        }


        if (safe.getThreshold() != _getCorrectThreshold()) {
            revert SignersCannotChangeThreshold();
        }


        // SENTINEL_OWNERS and SENTINEL_MODULES are both address(0x1)
        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount + 1);
        if (keccak256(abi.encode(modules)) != _existingModulesHash) {
            revert SignersCannotChangeModules();
        }


        // leave checked to catch underflows triggered by re-erntry attempts
        --_guardEntries;
    }
```

## Tool used
Manual Review

## Recommendation
The `HatsSignerGateBase` contract must account for modules that were enabled by other modules.

It must fetch the current number of modules in the `checkTransaction` function and then compare this number to the number of modules when the `checkAfterExecution` is executed.

Unfortunately the Safe does not provide an easy way to get the number of modules.

It only provides access to the modules via the [ModuleManager.getModulesPaginated](https://github.com/safe-global/safe-contracts/blob/131f0d25135c1b98c185c940ae37fb0275ac4062/contracts/base/ModuleManager.sol#L143-L175) function.

So I suggest to query `type(uint256).max` modules per page.

The changes that I propose are the following:
```diff
diff --git a/src/HatsSignerGateBase.sol b/src/HatsSignerGateBase.sol
index 3e8bb5f..540db1c 100644
--- a/src/HatsSignerGateBase.sol
+++ b/src/HatsSignerGateBase.sol
@@ -494,7 +494,7 @@ abstract contract HatsSignerGateBase is BaseGuard, SignatureDecoder, HatsOwnedIn
 
         // record existing modules for post-flight check
         // SENTINEL_OWNERS and SENTINEL_MODULES are both address(0x1)
-        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount);
+        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, type(uint256).max);
         _existingModulesHash = keccak256(abi.encode(modules));
 
         unchecked {
@@ -519,7 +519,7 @@ abstract contract HatsSignerGateBase is BaseGuard, SignatureDecoder, HatsOwnedIn
         }
 
         // SENTINEL_OWNERS and SENTINEL_MODULES are both address(0x1)
-        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, enabledModuleCount + 1);
+        (address[] memory modules,) = safe.getModulesPaginated(SENTINEL_OWNERS, type(uint256).max);
         if (keccak256(abi.encode(modules)) != _existingModulesHash) {
             revert SignersCannotChangeModules();
         }
```
Also you can remove the `enabledModuleCount` variable. It is no longer needed since we are now always querying `type(uint256).max` modules.