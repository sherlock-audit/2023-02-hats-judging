obront

medium

# buildHatId returns incorrect value for lowest level child

## Summary

If we call `buildHatId` with a level 14 hat (ie a hat with a full id that cannot have children) as the admin, rather than reverting (as it should) since they cannot have children, it returns a value.

## Vulnerability Detail

`buildHatId` is a public function that can be used to get the hatId of the child of a given hat. You input the admin hat and the `newHat` and it outputs the new hat's full ID:
```solidity
function buildHatId(uint256 _admin, uint16 _newHat) public pure returns (uint256 id) {
    uint256 mask;
    for (uint256 i = 0; i < MAX_LEVELS;) {
        unchecked {
            mask = uint256(
                type(uint256).max
                // should not overflow given known constants
                >> (TOPHAT_ADDRESS_SPACE + (LOWER_LEVEL_ADDRESS_SPACE * i))
            );
        }
        if (_admin & mask == 0) {
            unchecked {
                id = _admin
                    | (
                        uint256(_newHat)
                        // should not overflow given known constants
                        << (LOWER_LEVEL_ADDRESS_SPACE * (MAX_LEVELS - 1 - i))
                    );
            }
            return id;
        }

        // should not overflow based on < MAX_LEVELS stopping condition
        unchecked {
            ++i;
        }
    }
}
```
In the case that this function is called with an `adminHat` that is already at the lowest possible level, it should revert, since no children are possible. Instead, it returns a specific value:
```solidity
0x00000000.0000.0000.......0000
```

Here is a short test I wrote to illustrate this vulnerability:
```solidity
function testZach__BuildFinalHatId() public {
    uint full = type(uint).max;
    uint child = utils.buildHatId(full, 1);
    assert(child == 0);
}
```

## Impact

The `buildHatId` function returns an incorrect value instead of reverting when it's called with lowest level children. This could mislead users or connecting protocols into thinking a child hat will be produced when, in fact, it will not.

## Code Snippet

https://github.com/Hats-Protocol/hats-protocol/blob/fafcfdf046c0369c1f9e077eacd94a328f9d7af0/src/HatsIdUtilities.sol#L64-L91

## Tool used

Manual Review

## Recommendation

In the event that the no match is found, explicitly revert after the loop.