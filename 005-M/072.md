Allarious

high

# [High][Flow]`createHat` allows creation of hats without intermediary, which can lead to hats getting completely overwritten in the future

## Summary
`createHat` allows the creation of hats without checking their parent hat is created yet. This can lead to some states where is unexpected by the protocol and can lead to hats getting completely overriden by newly made ones.

## Vulnerability Detail
`createHat` function inside `Hats.sol` allows the creation of hats without checking if their parent hat is created yet. The only verification that is checked is `_checkAdmin`, which only analyzes the structure of the `hatId` to see if the `msg.sender` is wearing the admin hat at a certain level. The steps that this can malfunction happens as stated below:

(1) Top hat is created and `topHatWearer` is wearing it
(2) `topHatWearer` decides to create a hat at level 2, by passing the `LevelOneHatId` of a level 1 hat that is not created yet (either intentionally or by mistake)
(3) Hat at level 2 is created and `LevelOneHatId`, while not created yet, has its `lastHatId` set to 1
(4) `topHatWearer` decides to create a level 1 hat, which results in `LevelOneHatId` being actually created, this resets all the fields of the `LevelOneHatId` including `lastHatId`, which is reset to 0
(5) Now if new hats are created by `LevelOneHatId`, the new hats replace the old ones and all of the children data will be overriden.

### How can an admin abuse this?
While this can happen accidentally in the system, there is a method where malicious admins can abuse this. As stated in the documentation, immutable hats are supposed to be immutable since the wearers want to know that their responsibilities do not change in the future. This gives a backdoor to admins that allows complete override of the child hats in the future.

## Impact
The main impact is that the hats structure goes into a unpredictable state, things that can happen are such as:
* supply no longer counts the actual amount of users that are wearing the hat
* completely change the stored immutable hat structures

https://github.com/Hats-Protocol/hats-protocol/blob/fafcfdf046c0369c1f9e077eacd94a328f9d7af0/src/Hats.sol#L143-L170

## Code Snippet
```solidity
contract PoCTests is TestSetup {

    function setUp() public override {
        setUpVariables();

        hats = new Hats(name, _baseImageURI);
    }

    function test_createHatWithoutIntermediary() public {
        uint256 topHatUintId = 26959946667150639794667015087019630673637144422540572481103610249216; // 0x0000000100000000000000000000000000000000000000000000000000000000
        uint256 intermidiaryChild = 26960358043289970096177553829315270011263390106506980876069447401472; // 0x0000000100010000000000000000000000000000000000000000000000000000
        uint256 lastChild = 26960358049567071831564234593151059434471056522609336320533481914368; // 0x0000000100010001000000000000000000000000000000000000000000000000

        topHatId = hats.mintTopHat(topHatWearer, "tophat", "http://www.tophat.com/");
        assertEq(topHatId, topHatUintId);

        vm.startPrank(topHatWearer);
        uint256 newHat = hats.createHat(
            intermidiaryChild,
            "hat",
            _maxSupply,
            _eligibility,
            _toggle,
            false,
            "" // imageURI
        );

        /* Last child is created without the parent ever being created*/
        assertEq(newHat, lastChild);

        hats.mintHat(lastChild, secondWearer);
        vm.stopPrank();

        assertEq(hats.balanceOf(secondWearer, lastChild), 1); // The address is wearing the hat
        assertEq(hats.hatSupply(lastChild), 1);
        (,,,,,, uint16 lastHatId,,) = hats.viewHat(intermidiaryChild);
        assertEq(lastHatId, 1); // While intermidiaryChild is not made yet, its lastHatId is set to one!

        vm.prank(topHatWearer);
        hats.createHat( // creating intermidiaryHat
            topHatUintId,
            "hat",
            _maxSupply,
            _eligibility,
            _toggle,
            false,
            "" // imageURI
        );

        (,,,,,, lastHatId,,) = hats.viewHat(intermidiaryChild);
        assertEq(lastHatId, 0);
        /**
         * We see that this value is reset to 0, while a child exists. This can lead to an unstable state where the
         * protocol does not expect and many bad things might happen!
         * creation of new hats results in override of the children
         */
    }

}
```

## Tool used

Manual Review

## Recommendation
It is recommended to change the createHat function to check that the passed `_admin` address actually exists in the `_hats` mapping.